apply plugin: "java-library"
apply plugin: "maven-publish"
apply plugin: "signing"

String sonatypeUsername = findProperty("sonatypeUsername")
        ?.toString()
        ?: System.getenv("MAVEN_CENTRAL_USER")
String sonatypePassword = findProperty("sonatypePassword")
        ?.toString()
        ?: System.getenv("MAVEN_CENTRAL_PW")

afterEvaluate {
    publishing {
        repositories {
            maven {
                name "SonatypeMavenCentral"
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
                credentials {
                    username sonatypeUsername
                    password sonatypePassword
                }
            }
        }
        publications {
            release(MavenPublication) {
                from components.java

                groupId = 'co.infinum'
                artifactId = 'retromock'
                version = "$versions.retromock"

                artifact sourceJar
                artifact javadocJar

                pom {
                    name.set("Retromock")
                    description.set("Adapts Java interface created by Retrofit using annotations on declared methods to define response mocks.")
                    url.set("https://github.com/infinum/Retromock")
                    licenses {
                        license {
                            name.set("Apache License 2.0")
                            url.set("https://github.com/infinum/Retromock/blob/master/LICENSE")
                        }
                    }
                    developers {
                        developer {
                            id.set("antunflas")
                            name.set("Antun Fla≈°")
                            email.set("antun.flas@infinum.com")
                        }
                    }
                    scm {
                        connection.set("https://github.com/infinum/Retromock.git")
                        developerConnection.set("https://github.com/infinum/Retromock.git")
                        url.set("https://github.com/infinum/Retromock")
                    }
                }
                signing {
                    sign publishing.publications.release
                }
            }
        }
    }
}
