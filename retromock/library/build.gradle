plugins {
    id 'java'
    id 'java-library'
    id 'kotlin'
    id 'jacoco'
}

apply from: "$rootDir/config/quality.gradle"
apply from: "$rootDir/dokka.gradle"
apply from: "$rootDir/config.gradle"
apply from: "$rootDir/maven-publish.gradle"


java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

sourceSets {
    test.java.srcDirs += 'src/test/kotlin'
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

}

test {
    useJUnitPlatform()
    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
    }
    afterSuite { desc, result ->
        if (!desc.parent) {
            println "\nTest result: ${result.resultType}"
            println "Test summary: ${result.testCount} tests, " +
                    "${result.successfulTestCount} succeeded, " +
                    "${result.failedTestCount} failed, " +
                    "${result.skippedTestCount} skipped"
        }
    }
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.2"
}

dependencies {
    implementation deps.kotlin.stdlib
    implementation deps.kotlin.coroutines

    implementation deps.google.annotations

    implementation deps.retrofit

    testImplementation deps.junit.api
    testRuntimeOnly deps.junit.engine
    testImplementation deps.assertj
    testImplementation deps.mockito.core
    testImplementation deps.mockito.junit
    testImplementation group: 'com.google.guava', name: 'guava', version: '27.0-jre'
    testCompileOnly deps.google.annotations
    testImplementation deps.kotlin.coroutines

    testImplementation deps.kotlin.stdlib
    testImplementation deps.kotlin.test
}


clean {
    delete 'out'
}

apply from: "$rootDir/tasks.gradle"

assemble.dependsOn 'generateReadme'
